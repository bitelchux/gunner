class Game
  constructor: (game) ->
    @fireRate = 100
    @nextFire = 0
    @nextEnemy0 = 0
    @enemyRate0 = 220
    @nextEnemy1 = 0
    @enemyRate1 = 1000
    @ammo = 100
    @rewardExpire = 0
    @kills = 0
  
  ENEMY_COUNT: [50, 30]

  HIT_RATE:
    bullet:
      enemy0: 0.66
      enemy1: 0.30

  REWARD:
    enemy0: 2
    enemy1: 5

  preload: ->
    @game.load.image('sea', '<%= image_path('sea.png') %>')
    @game.load.image('crosshair', '<%= image_path('crosshair-small.png') %>')
    @game.load.image('turret', '<%= image_path('turret-small.png') %>')
    @game.load.image('bullet', '<%= image_path('bullet-small.png') %>')
    <% 2.times do |x| %>
    @game.load.spritesheet('enemy<%= x %>', '<%= image_path("enemy#{x}.png") %>', 32, 32)
    @game.load.spritesheet('reward<%= x %>', '<%= image_path("reward#{x}.png") %>', 32, 32, 10)
    <% end %>
    @game.load.spritesheet('explosion', '<%= image_path("explosion.png") %>', 32, 32, 6)
    @game.load.image('deck', '<%= image_path('deck.png') %>')
    return

  fire: ->
    if (@ammo > 0 and @game.time.now > @nextFire && @bullets.countDead() > 0 and @game.input.activePointer.y < 520)
      @nextFire = @game.time.now + @fireRate
      @bullet = @bullets.getFirstExists(false)
      @bullet.reset(400, 525)
      @bullet.rotation = @game.physics.arcade.moveToPointer(@bullet, 200, @game.input.activePointer)
      @ammo--
      @ammoText.text = @ammo

  create: ->
    @land = @game.add.tileSprite(0, 0, 800, 600, 'sea')

    @enemies = []
    for i in [0...@ENEMY_COUNT.length]
      enemies = @game.add.group()
      enemies.enableBody = true
      enemies.physicsBodyType = Phaser.Physics.ARCADE
      enemies.createMultiple(@ENEMY_COUNT[i], 'enemy' + i, 0, false)
      enemies.setAll('anchor.x', 0.5)
      enemies.setAll('anchor.y', 0.5)
      enemies.forEach( (enemy) ->
        enemy.animations.add('hit', [ 3, 2, 3, 2 ], 20, false)
        enemy.animations.add('fly', [ 0, 1, 2 ], 20, true)
        enemy.events.onAnimationComplete.add( (enemy) ->
          enemy.play('fly')
        , this)
      , this)
      @enemies.push(enemies)

    @explosions = @game.add.group()
    @explosions.createMultiple(50, 'explosion')
    @explosions.setAll('animations.add', 'explosion')
    @explosions.forEach(((explosion) ->
      explosion.animations.add 'explosion'
    ), this)

    @rewards = []
    for i in [0...@ENEMY_COUNT.length]
      rewards = @game.add.group()
      rewards.createMultiple(50, 'reward' + i)
      rewards.forEach(((reward) ->
        reward.animations.add 'reward' + i
      ), this)
      @rewards.push(rewards)

    @deck = @game.add.sprite(0, 520, 'deck')

    @bullets = @game.add.group()
    @bullets.enableBody = true
    @bullets.physicsBodyType = Phaser.Physics.ARCADE
    @bullets.createMultiple(50, 'bullet', 0, false)
    @bullets.setAll('anchor.x', 0.5)
    @bullets.setAll('anchor.y', 0.5)
    @bullets.setAll('outOfBoundsKill', true)
    @bullets.setAll('checkWorldBounds', true)

    @turret = @game.add.sprite(400, 525, 'turret')
    @turret.anchor.setTo(0.3, 0.5)

    @ammoText = @game.add.text(400, 545, @ammo, { font: '14px monospace', fill: '#fff' })
    @ammoText.anchor.setTo(0.5, 0.5)

    @rewardText = @game.add.text(400, 565, "", { font: '14px monospace', fill: '#fff' })
    @rewardText.anchor.setTo(0.5, 0.5)

    @killText = @game.add.text(20, 545, "Kills: 0", { font: '14px monospace', fill: '#fff' })
    @killText.anchor.setTo(0, 0.5)
    return

  update: ->
    @fire() if @game.input.activePointer.isDown
    @land.tilePosition.y += 0.5
    @turret.rotation = game.physics.arcade.angleToPointer(@turret)
    for i in [0...@ENEMY_COUNT.length]
      @game.physics.arcade.overlap(@bullets, @enemies[i], @collisionHandler, null, this)
      @enemies[i].forEach( (enemy) ->
        enemy.kill() if enemy.y > 530
      , this)

  render: ->
    #console.log "#{game.time.now} #{nextEnemy0} #{enemies.countDead()}"
    if @game.time.now > @rewardExpire
      @rewardText.text = ""
    if @game.time.now > @nextEnemy0 and @enemies[0].countDead() > 0
      @nextEnemy0 = @game.time.now + @enemyRate0
      enemy = @enemies[0].getFirstExists(false)
      enemy.reset(20 + Math.random() * 760, 0)
      enemy.body.velocity.y = 30 + Math.random() * 30
      enemy.play('fly')

    if @game.time.now > @nextEnemy1 and @enemies[1].countDead() > 0
      @nextEnemy1 = @game.time.now + @enemyRate1
      enemy = @enemies[1].getFirstExists(false)
      if Math.random() < 0.5
        enemy.reset(0, 20 + Math.random() * 320)
        enemy.body.velocity.x = 40 + Math.random() * 40
        enemy.rotation = -Math.PI / 2
      else
        enemy.reset(800, 20 + Math.random() * 320)
        enemy.body.velocity.x = -(40 + Math.random() * 40)
        enemy.rotation = Math.PI / 2
      enemy.body.velocity.y = 30
      enemy.play('fly')

    return

  collisionHandler: (bullet, enemy) ->
    bullet.kill()
    if Math.random() < @HIT_RATE[bullet.key][enemy.key]
      i = parseInt(enemy.key.slice(5))
      enemy.kill()
      explosion = @explosions.getFirstExists(false)
      explosion.reset(enemy.body.x, enemy.body.y)
      explosion.play('explosion', 30, false, true)
      reward = @rewards[i].getFirstExists(false)
      reward.reset(enemy.body.x, enemy.body.y)
      reward.play('reward' + i, 30, false, true)
      @ammo += @REWARD[enemy.key]
      @ammoText.text = @ammo
      @rewardText.text = '+' + @REWARD[enemy.key]
      @rewardExpire = @game.time.now + 200
      @kills++
      @killText.text = "Kills: " + @kills
    else
      enemy.play('hit')

game = new Phaser.Game(800, 600, Phaser.AUTO, 'game-container')
game.state.add('Game', Game)

game.state.start('Game')
