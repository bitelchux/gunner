class Game
  constructor: (game) ->
    @nextFire =
      small: 0
      large: 0
      burst: 0

    @nextEnemy0 = 0
    @enemyRate0 = 220
    @nextEnemy1 = 0
    @enemyRate1 = 1000
    @ammo = 100
    @rewardExpire = 0
    @kills = 0
    @selectedBullet = 'small'

  BULLET_TYPES: ['small', 'large', 'burst']

  ENEMY_COUNT: [50, 30]

  AMMO_REQ:
    small: 1
    large: 3
    burst: 8

  BULLET_OFFSET:
    small: 20
    large: 30
    burst: 35

  FIRE_RATE:
    small: 100
    large: 150
    burst: 250

  HIT_RATE:
    "bullet-small":
      enemy0: 0.66
      enemy1: 0.30
    "bullet-large":
      enemy0: 1
      enemy1: 0.9
    "bullet-burst":
      enemy0: 1
      enemy1: 1

  REWARD:
    enemy0: 2
    enemy1: 5

  preload: ->
    @game.load.image('sea', '<%= image_path('sea.png') %>')
    @game.load.image('crosshair', '<%= image_path('crosshair-small.png') %>')
    <% %w{small large burst}.each do |x| %>
    @game.load.image('turret-<%= x %>', '<%= image_path("turret-#{x}.png") %>')
    @game.load.image('bullet-<%= x %>', '<%= image_path("bullet-#{x}.png") %>')
    <% end %>
    <% 2.times do |x| %>
    @game.load.spritesheet('enemy<%= x %>', '<%= image_path("enemy#{x}.png") %>', 32, 32)
    @game.load.spritesheet('reward<%= x %>', '<%= image_path("reward#{x}.png") %>', 32, 32, 10)
    <% end %>
    @game.load.spritesheet('explosion', '<%= image_path("explosion.png") %>', 32, 32, 6)
    @game.load.image('deck', '<%= image_path('deck.png') %>')
    return

  fire: ->
    bullets = @bullets[@BULLET_TYPES.indexOf(@selectedBullet)]
    ammoReq = @AMMO_REQ[@selectedBullet]
    if (@ammo >= ammoReq and @game.time.now > @nextFire[@selectedBullet] && bullets.countDead() > 0 and @game.input.activePointer.y < 520)
      @nextFire[@selectedBullet] = @game.time.now + @FIRE_RATE[@selectedBullet]
      bullet = bullets.getFirstExists(false)
      angle = @game.physics.arcade.angleToPointer(@turrets[@selectedBullet])
      offset = @BULLET_OFFSET[@selectedBullet]
      bullet.reset(400 + Math.cos(angle) * offset, 525 + Math.sin(angle) * offset)
      bullet.rotation = @game.physics.arcade.moveToPointer(bullet, 200, @game.input.activePointer)
      @ammo-= ammoReq
      @ammoText.text = @ammo

  create: ->
    @land = @game.add.tileSprite(0, 0, 800, 600, 'sea')

    @enemies = []
    for i in [0...@ENEMY_COUNT.length]
      enemies = @game.add.group()
      enemies.enableBody = true
      enemies.physicsBodyType = Phaser.Physics.ARCADE
      enemies.createMultiple(@ENEMY_COUNT[i], 'enemy' + i, 0, false)
      enemies.setAll('anchor.x', 0.5)
      enemies.setAll('anchor.y', 0.5)
      enemies.forEach( (enemy) ->
        enemy.animations.add('hit', [ 3, 2, 3, 2 ], 20, false)
        enemy.animations.add('fly', [ 0, 1, 2 ], 20, true)
        enemy.events.onAnimationComplete.add( (enemy) ->
          enemy.play('fly')
        , this)
      , this)
      @enemies.push(enemies)

    @explosions = @game.add.group()
    @explosions.createMultiple(50, 'explosion')
    @explosions.setAll('animations.add', 'explosion')
    @explosions.forEach(((explosion) ->
      explosion.animations.add 'explosion'
    ), this)

    @rewards = []
    for i in [0...@ENEMY_COUNT.length]
      rewards = @game.add.group()
      rewards.createMultiple(50, 'reward' + i)
      rewards.forEach(((reward) ->
        reward.animations.add 'reward' + i
      ), this)
      @rewards.push(rewards)

    @deck = @game.add.sprite(0, 520, 'deck')

    @bullets = []
    for bullet in @BULLET_TYPES
      bullets = @game.add.group()
      bullets.enableBody = true
      bullets.physicsBodyType = Phaser.Physics.ARCADE
      console.log 'bullet-' + bullet
      bullets.createMultiple(50, 'bullet-' + bullet, 0, false)
      bullets.setAll('anchor.x', 0.5)
      bullets.setAll('anchor.y', 0.5)
      bullets.setAll('outOfBoundsKill', true)
      bullets.setAll('checkWorldBounds', true)
      @bullets.push bullets

    @turrets = {}
    for i in @BULLET_TYPES
      @turrets[i] = @game.add.sprite(400, 525, 'turret-' + i)
      @turrets[i].anchor.setTo(0.3, 0.5)
      unless i is @selectedBullet
        @turrets[i].kill()

    @ammoText = @game.add.text(400, 545, @ammo, { font: '14px monospace', fill: '#fff' })
    @ammoText.anchor.setTo(0.5, 0.5)

    @rewardText = @game.add.text(400, 565, "", { font: '14px monospace', fill: '#fff' })
    @rewardText.anchor.setTo(0.5, 0.5)

    @killText = @game.add.text(20, 545, "Kills: 0", { font: '14px monospace', fill: '#fff' })
    @killText.anchor.setTo(0, 0.5)
    return

  update: ->
    @fire() if @game.input.activePointer.isDown
    bulletChanged = true
    if @game.input.keyboard.isDown(Phaser.Keyboard.ONE) and not (@selectedBullet is 'small')
      @selectedBullet = 'small'
    else if @game.input.keyboard.isDown(Phaser.Keyboard.TWO) and not (@selectedBullet is 'large')
      @selectedBullet = 'large'
    else if @game.input.keyboard.isDown(Phaser.Keyboard.THREE) and not (@selectedBullet is 'burst')
      @selectedBullet = 'burst'
    else
      bulletChanged = false

    if bulletChanged
      for i in @BULLET_TYPES
        unless i is @selectedBullet
          @turrets[i].kill()
      @turrets[@selectedBullet].reset(400, 525)

    @land.tilePosition.y += 0.5
    @turrets[@selectedBullet].rotation = @game.physics.arcade.angleToPointer(@turrets[@selectedBullet])
    for i in [0...@ENEMY_COUNT.length]
      for j in [0...@BULLET_TYPES.length]
        @game.physics.arcade.overlap(@bullets[j], @enemies[i], @collisionHandler, null, this)
      @enemies[i].forEach( (enemy) ->
        enemy.kill() if enemy.y > 530
      , this)

  render: ->
    #console.log "#{game.time.now} #{nextEnemy0} #{enemies.countDead()}"
    if @game.time.now > @rewardExpire
      @rewardText.text = ""
    if @game.time.now > @nextEnemy0 and @enemies[0].countDead() > 0
      @nextEnemy0 = @game.time.now + @enemyRate0
      enemy = @enemies[0].getFirstExists(false)
      enemy.reset(20 + Math.random() * 760, 0)
      enemy.body.velocity.y = 30 + Math.random() * 30
      enemy.play('fly')

    if @game.time.now > @nextEnemy1 and @enemies[1].countDead() > 0
      @nextEnemy1 = @game.time.now + @enemyRate1
      enemy = @enemies[1].getFirstExists(false)
      if Math.random() < 0.5
        enemy.reset(0, 20 + Math.random() * 320)
        enemy.body.velocity.x = 40 + Math.random() * 40
        enemy.rotation = -Math.PI / 2
      else
        enemy.reset(800, 20 + Math.random() * 320)
        enemy.body.velocity.x = -(40 + Math.random() * 40)
        enemy.rotation = Math.PI / 2
      enemy.body.velocity.y = 30
      enemy.play('fly')

    return

  collisionHandler: (bullet, enemy) ->
    if bullet.key is 'bullet-burst'
      bullets = @bullets[0]
      angle = (Math.random() * 2 * Math.PI) - Math.PI
      for i in [1..5]
        b = bullets.getFirstExists(false)
        b.reset(bullet.x, bullet.y)
        b.body.velocity.x = Math.cos(angle * i) * 200
        b.body.velocity.y = Math.sin(angle * i) * 200

    bullet.kill()
    if Math.random() < @HIT_RATE[bullet.key][enemy.key]
      i = parseInt(enemy.key.slice(5))
      enemy.kill()
      explosion = @explosions.getFirstExists(false)
      explosion.reset(enemy.body.x, enemy.body.y)
      explosion.play('explosion', 30, false, true)
      reward = @rewards[i].getFirstExists(false)
      reward.reset(enemy.body.x, enemy.body.y)
      reward.play('reward' + i, 30, false, true)
      @ammo += @REWARD[enemy.key]
      @ammoText.text = @ammo
      @rewardText.text = '+' + @REWARD[enemy.key]
      @rewardExpire = @game.time.now + 200
      @kills++
      @killText.text = "Kills: " + @kills
    else
      enemy.play('hit')

game = new Phaser.Game(800, 600, Phaser.AUTO, 'game-container')
game.state.add('Game', Game)

game.state.start('Game')
